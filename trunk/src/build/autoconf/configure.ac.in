## 
## This file is part of RGL
##
## use src/build/Maintainer.mk to update configure.ac
## Process this file with autoconf to produce a configure script.
## origin: src/build/autoconf/configure.ac.in
##
## $Id: configure.ac.in,v 1.6 2003/11/16 14:07:33 dadler Exp $
##

AC_PREREQ(2.50)


## ---[ VERSION ]-------------------------------------------------------------

AC_INIT([rgl], [@VERSION@], [dadler@uni-goettingen.de])
AC_CONFIG_AUX_DIR(src/build/autoconf)

## ---[ check host ]----------------------------------------------------------

AC_CANONICAL_HOST

case "${host_os}" in
  macos*)
    echo "MacOS port not supported yet."
    exit -1
  ;;
  cygwin*|mingw*|windows*|winnt)

    echo "Windows port: use R CMD INSTALL to build (configure.win)"
    exit -1
  ;;
  *)
  ;;
esac


##
## ===[ X11 Port ]============================================================
##

## C language

CPPFLAGS=""
CFLAGS="-Wall -pedantic"


## C++ language

LIBS="-lstdc++"
CXXFLAGS="${CFLAGS} -fno-exceptions -fno-rtti"


## X11 backend

AC_PATH_X
LIBS="${LIBS} -L${x_libraries} -lX11 -lXext"
CPPFLAGS="${CPPFLAGS} -I${x_includes}"

## OpenGL backend

AC_ARG_WITH(gl-includes,
[  --with-gl-includes=DIR  specify location of OpenGL headers],
[CPPFLAGS="${CPPFLAGS} -I${withval}"],
)

AC_ARG_WITH(gl-libs,
[  --with-gl-libs=DIR      specify location of OpenGL libs],
[LIBS="${LIBS} -L${withval}"]
)

AC_ARG_WITH(gl-libname,
[  --with-gl-libnae=NAME specify Library name (defaults to "GL")], 
[LIBS="${LIBS} -l${withval}"], [LIBS="${LIBS} -lGL"]
)

## LibPNG support

AC_ARG_WITH(libpng-includes,
[  --with-libpng-includes=DIR specify location of libpng headers],
[CPPFLAGS="${CPPFLAGS} -I${withval}"],
)
AC_ARG_WITH(libpng-libs,
[  --with-libpng-libs=DIR     specify location of libpng library],
[LIBS="${LIBS} -L${withval}"]
)
AC_ARG_ENABLE(libpng,
[  --disable-libpng           compile without PNG image support])
AC_MSG_RESULT([$enable_libpng])
if test "x$enable_libpng" != "xno"; then
AC_CHECK_HEADERS(png.h,CPPFLAGS="${CPPFLAGS} -DHAVE_PNG_H=1"; LIBS="${LIBS} ${PNGLIB:=-lpng}")
fi

## Darwin/MacOS X stuff

if test `uname` = "Darwin" ; then
  CPPFLAGS="${CPPFLAGS} -DDarwin"
  CFLAGS="${CFLAGS} -DDarwin"
  # Apple's OpenGL is different from the X11 one - it must be loaded *before* X11
  if test -e /System/Library/Frameworks/OpenGL.framework ; then
    CPPFLAGS="-I/System/Library/Frameworks/OpenGL.framework/Headers ${CPPFLAGS} -DNO_GL_PREFIX"
    LIBS="-framework OpenGL ${LIBS}"
  fi
  # X11 must come *after* the OpenGL stuff
  CPPFLAGS="${CPPFLAGS} -I/usr/X11R6/include"
fi

## Output

## AC_SUBST(PLATFORM_MODULES)
AC_SUBST(CPPFLAGS)
AC_SUBST(CFLAGS)
AC_SUBST(CXXFLAGS)
AC_SUBST(LIBS)
    
AC_OUTPUT(
src/Makefile
src/Makevars
)
